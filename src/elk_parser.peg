
template <- (tag / text)*;

tag <-
    nl:NL?
    tag:(
        self /
        block_start /
        inverse_start /
        block_end /
        var_raw1 /
        var_raw2 /
        partial /
        comment /
        var);

text <- (!tag .)+;

self <-
    prefix:WS* key:"{{.}}" postfix:(WS* NL)?;

var <-
    prefix:WS* "{{"  WS* key:dotted_id WS* "}}" postfix:(WS* NL)?;

var_raw1 <-
    prefix:WS* "{{{" WS* key:dotted_id WS* "}}}" postfix:(WS* NL)?;

var_raw2 <-
    prefix:WS* "{{&" WS* key:dotted_id WS* "}}" postfix:(WS* NL)?;

block_start <-
    prefix:WS* "{{#" WS* key:dotted_id WS* "}}" postfix:(WS* NL)?;

inverse_start <-
    prefix:WS* "{{^" WS* key:dotted_id WS* "}}" postfix:(WS* NL)?;

block_end <-
    prefix:WS* "{{/" WS* key:dotted_id WS* "}}" postfix:(WS* NL)?;

partial <-
    prefix:WS* "{{>" WS* key:id WS* "}}" postfix:(WS* NL)?;

comment <-
    prefix:WS* "{{!" (!"}}" .)* "}}" postfix:(WS* NL)?;

dotted_id <-
    first_id:[^\s{}\.]+ ("." id:[^\s{}\.]+)*;

id <- [^\s{}]+;
WS <- [\t ];
NL <- "\r"? "\n";
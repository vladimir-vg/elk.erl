
template <- item* (nl item*)*;

nl <- "\r"? "\n";
item <- tag / text;
tag <-
  self /
  block_start /
  inverse_start /
  block_end /
  raw_var1 /
  raw_var2 /
  partial /
  comment /
  var ;

text <- (!tag !nl .)+;

self          <- "{{.}}";
var           <- "{{"  WS* key:dotted_id WS* "}}";
raw_var1      <- "{{{" WS* key:dotted_id WS* "}}}";
raw_var2      <- "{{&" WS* key:dotted_id WS* "}}";
block_start   <- "{{#" WS* key:dotted_id WS* "}}";
inverse_start <- "{{^" WS* key:dotted_id WS* "}}";
block_end     <- "{{/" WS* key:dotted_id WS* "}}";
partial       <- "{{>" WS* key:id WS* "}}";
comment       <- "{{!" (!"}}" .)* "}}";

dotted_id <- first_id:[^\s{}\.]+ ("." id:[^\s{}\.]+)*;

id <- [^\s{}]+;
WS <- [\t ];